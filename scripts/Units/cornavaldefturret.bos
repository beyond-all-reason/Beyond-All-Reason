
#include "../recoil_common_includes.h"

piece
base,
turretStrut,
turretHeadingPivot,
turretPitchPivot,
barrel,
barrelFlare,
firingPin;


// static-var  Static_Var_1, Static_Var_2, Static_Var_3, Static_Var_4, Static_Var_5,
//             Static_Var_6, Static_Var_7, restore_delay, wpn1_lasthead;

static-var restore_delay, wpn1_lasthead;

// Signal definitions
#define SIG_AIM				2

#define WATER_ROCK_UNITSIZE 10
#include "../floatmotion.h"


#define BASEPIECE base
#define HITSPEED <20.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 5
#define MAXTILT 100
#define RECOIL_POWER 20000

#include "unit_hitbyweaponid_and_smoke.h"


Create()
{
    move firingPin to y-axis [1] now;
	restore_delay = 2000;
	start-script FloatMotion();
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	turn turretHeadingPivot to y-axis <0> speed <60>;
    turn turretPitchPivot to x-axis <0.000000> speed <30>;
	wpn1_lasthead = 1000000;
	set-signal-mask 0;
}

SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}

RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turretHeadingPivot to y-axis heading speed <100>;
	turn turretPitchPivot to x-axis <0.000000> - pitch speed <60>;

    if(pitch < <-20>){
        move turretPitchPivot to y-axis [8] speed [16];
    }
    else{
        move turretPitchPivot to y-axis [0] speed [16];
    }

	if (((get ABS(wpn1_lasthead - heading)) > 65536) OR(((get ABS(wpn1_lasthead - heading)) > 1300) AND ((get ABS(wpn1_lasthead - heading)) < 64236)))
	{
		wpn1_lasthead = 1000000;
		wait-for-turn turretHeadingPivot around y-axis;
		wait-for-turn turretPitchPivot around x-axis;
	}
	wpn1_lasthead = heading;
	start-script RestoreAfterDelay();
	return (1);
}

FirePrimary()
{
    emit-sfx 1024 + 0 from barrelFlare;
    move barrel to z-axis [-7] now;
    move firingPin to z-axis [-3] now;
    sleep 100;
    move barrel to z-axis [0] speed [7];
    sleep 800;
    move firingPin to z-axis [1] speed [12];
    wait-for-move firingPin along z-axis;
    move firingPin to z-axis [0] speed [4];
}

// Shot1(zero)
// {

// }

AimFromPrimary(piecenum)
{
	piecenum = turretStrut;
}

QueryPrimary(piecenum)
{
    piecenum = barrelFlare;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turretHeadingPivot type BITMAPONLY | NOHEATCLOUD;
		explode turretPitchPivot type BITMAPONLY | NOHEATCLOUD;
		explode barrel type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrelFlare type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turretHeadingPivot type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turretPitchPivot type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrel type FALL | NOHEATCLOUD;
		explode barrelFlare type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turretHeadingPivot type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turretPitchPivot type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrel type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrelFlare type SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turretHeadingPivot type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turretPitchPivot type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrel type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode barrelFlare type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
