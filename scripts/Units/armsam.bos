
#include "../recoil_common_includes.h"

piece  flare1, flare2, base, turret, lwheel, rwheel, mlauncher;

static-var  restore_delay, gun_1, fired, aiming;

// Signal definitions
#define SIGNAL_MOVE 1
#define SIGNAL_AIM1 256
#define SIGNAL_AIM2 512
#define SIGNAL_IDLE 8 
#define SIGNAL_SHOOT1 16


#define BASEPIECE base
#define HITSPEED <75.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 1
#define MAXTILT 100
#define RECOIL_POWER 5000
#include "../unit_hitbyweaponid_and_smoke.h"

#define TB_BASE base 
#define TB_TURNRATE <15.0> 
#define TB_TILT_X <0.10>
#define TB_BANK_Z <0.10> // Do not define this if you dont want banking
#include "../tilt_bank_mobileunit.h"


Create()
{
	hide flare1;
	hide flare2;
	gun_1 = flare1;
	fired = 0;
	aiming = 0;
	restore_delay = 3000;
	call-script TB_Init();
}

SetMaxReloadTime(reloadMS)
{
	restore_delay = reloadMS * 2;
}

ResetFire()
{
	signal SIGNAL_SHOOT1;
	set-signal-mask SIGNAL_SHOOT1;
	fired = 1;
	sleep 3000;
	fired = 0;
}

ResetAiming()
{
	signal SIGNAL_IDLE;
	set-signal-mask SIGNAL_IDLE;
	aiming = 1;
	sleep 500;
	aiming = 0;
}


static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	fired = 0;
	turn turret to y-axis <0.0> speed <90.021978>;
}
SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

AimWeapon1(heading, pitch)
{
	signal SIGNAL_AIM1;
	set-signal-mask SIGNAL_AIM1;
	start-script ResetAiming();
	turn turret to y-axis heading speed <250>;
	turn mlauncher to x-axis -1*pitch speed <200>;
	wait-for-turn turret around y-axis;
	wait-for-turn mlauncher around x-axis;
	start-script RestoreAfterDelay();
	if(fired == 1)
	{
		return (0);
	}
	return (1);
}

FireWeapon1()
{
	start-script ResetFire();
	gun_1 = !gun_1;
	return (0);
}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = flare1;
}

QueryWeapon1(pieceIndex)
{
	pieceIndex = flare1 + gun_1;
}

AimWeapon2(heading, pitch)
{
	signal SIGNAL_AIM2;
	set-signal-mask SIGNAL_AIM2;
	if(aiming == 1)
	{
		return (0);
	}
	turn turret to y-axis heading speed <250>;
	turn mlauncher to x-axis -1*pitch speed <200>;
	wait-for-turn turret around y-axis;
	wait-for-turn mlauncher around x-axis;
	start-script RestoreAfterDelay();
	if(fired == 1)
	{
		return (0);
	}
	return (1);
}

FireWeapon2()
{
	start-script ResetFire();
	gun_1 = !gun_1;
	return (0);
}

AimFromWeapon2(piecenum)
{
	piecenum = flare1;
}

QueryWeapon2(piecenum)
{
	piecenum = flare1 + gun_1;
}

StartMoving(reversing)
{
	spin lwheel around x-axis speed <150> accelerate <15>;
	spin rwheel around x-axis speed <150> accelerate <15>;
	signal SIGNAL_MOVE;
	set-signal-mask SIGNAL_MOVE;

	START_TILTBANK;
}
StopMoving()
{
	stop-spin rwheel around x-axis;
	stop-spin lwheel around x-axis;
	
	signal SIGNAL_MOVE;
	STOP_TILTBANK;
}
Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rwheel type BITMAPONLY | NOHEATCLOUD;
		explode lwheel type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret type FALL | NOHEATCLOUD;
		explode rwheel type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode lwheel type FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rwheel type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode lwheel type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rwheel type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode lwheel type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
