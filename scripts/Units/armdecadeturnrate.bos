
#include "../recoil_common_includes.h"

piece  base, turret1, turret2, abarrela, abarrelb, bbarrela, bbarrelb, flare11,
       flare12, flare21, flare22, ground;

static-var  gun_1, gun_2, restore_delay, oldHead;

// Signal definitions
#define SIGNAL_AIM1 256
#define SIGNAL_AIM2 512
#define SIGNAL_MOVE 1

#define RB_MASS 20
#define RB_LENGTH 5
#define RB_WIDTH 3
#define RB_PITCH_SPEED 200
#define RB_PITCH_ACCELERATION 20
#define RB_ROLL_ACCELERATION 8
#define RB_RECOIL_ENERGY_2 500
#define RB_WAKE_PIECE ground
#define RB_WAKE_CEG 1024 + 1
#define RB_IDLE_KICK 10000

#include "../bar_ships_common.h"

Create()
{
	hide flare21;
	hide flare22;
	hide flare11;
	hide flare12;
	hide ground;
	hide flare11;
	hide flare12;
	hide flare21;
	hide flare22;
	gun_1 = base;
	gun_2 = base;
	restore_delay = 3000;
	start-script InitRockBoat();
	SLEEP_UNTIL_UNITFINISHED;
	start-script BoatPhysics();
}

SetMaxReloadTime(reloadMS)
{
	restore_delay = reloadMS * 2;
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	turn turret1 to y-axis <0.> speed <90.>;
	turn turret2 to y-axis <0.> speed <90.>;
	turn abarrela to x-axis <0.> speed <50.>;
	turn abarrelb to x-axis <0.> speed <50.>;
	turn bbarrela to x-axis <0.> speed <50.>;
	turn bbarrelb to x-axis <0.> speed <50.>;
}
SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

StartMoving(reversing)
{
}

StopMoving()
{
}

AimWeapon1(heading, pitch)
{
	signal SIGNAL_AIM1;
	set-signal-mask SIGNAL_AIM1;
	turn turret1 to y-axis heading speed <475.>;
	turn abarrela to x-axis <0.> - pitch speed <150.>;
	turn abarrelb to x-axis <0.> - pitch speed <150.>;
	wait-for-turn turret1 around y-axis;
	wait-for-turn abarrela around x-axis;
	wait-for-turn abarrelb around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FireWeapon1()
{
	if( gun_1 )
	{
		move abarrelb to z-axis [-0.33] speed [500.];
	    emit-sfx 1024 + 0 from flare12;
		sleep 100;
		move abarrelb to z-axis [-0.66] speed [500.];
		emit-sfx 1024 + 0 from flare12;
		sleep 100;
		move abarrelb to z-axis [-1.] speed [500.];
		emit-sfx 1024 + 0 from flare12;
		wait-for-move abarrelb along z-axis;
		move abarrelb to z-axis [0.] speed [10];
	}
	else
	{
		move abarrela to z-axis [-0.33] speed [500.];
	    emit-sfx 1024 + 0 from flare11;
		sleep 100;
		move abarrela to z-axis [-0.66] speed [500.];
		emit-sfx 1024 + 0 from flare11;
		sleep 100;
		move abarrela to z-axis [-1.] speed [500.];
		emit-sfx 1024 + 0 from flare11;
		wait-for-move abarrela along z-axis;
		move abarrela to z-axis [0.] speed [10];
	}
	gun_1 = !gun_1;
}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = turret1;
}

AimWeapon2(heading, pitch)
{
	signal SIGNAL_AIM2;
	set-signal-mask SIGNAL_AIM2;
	turn turret2 to y-axis heading speed <475.>;
	turn bbarrela to x-axis <0.> - pitch speed <150.>;
	turn bbarrelb to x-axis <0.> - pitch speed <150.>;
	wait-for-turn turret2 around y-axis;
	wait-for-turn bbarrela around x-axis;
	wait-for-turn bbarrelb around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FireWeapon2()
{
	if( gun_2 )
	{
		move bbarrelb to z-axis [-0.33] speed [500.];
	    emit-sfx 1024 + 0 from flare22;
		sleep 100;
		move bbarrelb to z-axis [-0.66] speed [500.];
		emit-sfx 1024 + 0 from flare22;
		sleep 100;
		move bbarrelb to z-axis [-1.] speed [500.];
		emit-sfx 1024 + 0 from flare22;
		wait-for-move bbarrelb along z-axis;
		move bbarrelb to z-axis [0.] speed [10];
	}
	else
	{
		move bbarrela to z-axis [-0.33] speed [500.];
	    emit-sfx 1024 + 0 from flare21;
		sleep 100;
		move bbarrela to z-axis [-0.66] speed [500.];
		emit-sfx 1024 + 0 from flare21;
		sleep 100;
		move bbarrela to z-axis [-1.] speed [500.];
		emit-sfx 1024 + 0 from flare21;
		wait-for-move bbarrela along z-axis;
		move bbarrela to z-axis [0.] speed [10];
	}
	gun_2 = !gun_2;
}

AimFromWeapon2(pieceIndex)
{
	pieceIndex = turret2;
}

QueryWeapon1(pieceIndex)
{
	pieceIndex = flare11 + gun_1;
}

QueryWeapon2(pieceIndex)
{
	pieceIndex = flare21 + gun_2;
}



Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode ground type BITMAPONLY | NOHEATCLOUD;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret1 type BITMAPONLY | NOHEATCLOUD;
		explode abarrelb type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode abarrela type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode ground type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode abarrelb type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode abarrela type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode ground type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode abarrelb type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode flare12 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode abarrela type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode ground type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode base type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode turret1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode abarrelb type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare12 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode abarrela type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
