
#include "../recoil_common_includes.h"

piece  flare1, base, wing1, wing2, thrusta, thrustb, thrustc, bomb1;
static-var inAileronRoll, active;


#define BASEPIECE base
#define HITSPEED <55.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 3
#define MAXTILT 100

MoveRate1() {
	active = get(GAME_FRAME);
}


MoveRate2()
{
	active = get(GAME_FRAME);
	if( Rand( 1, 10 ) == 1 AND ( !inAileronRoll ) )
	{
		inAileronRoll = 1;
		turn base to z-axis <240.049451> speed <120.027473>;
		wait-for-turn base around z-axis;
		turn base to z-axis <120.027473> speed <180.038462>;
		wait-for-turn base around z-axis;
		turn base to z-axis <0.0> speed <120.027473>;
		inAileronRoll = 0;
	}
}

Create()
{
	hide thrustc;
	hide thrusta;
	hide thrustb;
	hide flare1;
	active = 0;
	inAileronRoll = 0;
}

ReloadBomb() {
	sleep 9000;
	show bomb1;
}

Activate()
{
	active = get(GAME_FRAME);
	show thrustc;
	show thrusta;
	show thrustb;
	move wing1 to x-axis [-2.4] speed [2.4];
	move wing1 to z-axis [-0.5] speed [0.5];
	move wing2 to x-axis [2.4] speed [2.4];
	move wing2 to z-axis [-0.5] speed [0.5];
	turn wing1 to z-axis <0.89> speed <0.89>;
	turn wing2 to z-axis <-2.69> speed <2.69>;
}

Deactivate()
{
	active = 0;
	hide thrustc;
	hide thrusta;
	hide thrustb;
	move wing1 to x-axis [0.0] speed [2.4];
	move wing1 to z-axis [0.0] speed [0.5];
	move wing2 to x-axis [0.0] speed [2.4];
	move wing2 to z-axis [0.0] speed [0.5];
	turn wing1 to z-axis <0.0> speed <0.89>;
	turn wing2 to z-axis <0.0> speed <2.69>;
}

FireWeapon1()
{
	hide bomb1;
	start-script ReloadBomb();
}

AimWeapon1()
{
	if (active < (get(GAME_FRAME) - 60)) {
		return (1);//only shoot if we've been flying for more than a second
	} else {
		return (0);
 	}
}

QueryWeapon1(pieceIndex)
{
	pieceIndex = flare1;
}



Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode thrusta type BITMAPONLY | NOHEATCLOUD;
		explode flare1 type FALL | BITMAP2 | NOHEATCLOUD;
		return (corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode flare1 type FALL | BITMAP2 | NOHEATCLOUD;
		explode thrustc type BITMAPONLY | NOHEATCLOUD;
		explode wing1 type BITMAPONLY | NOHEATCLOUD;
		explode thrusta type BITMAPONLY | NOHEATCLOUD;
		explode thrustb type BITMAPONLY | NOHEATCLOUD;
		return (corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode flare1 type BITMAPONLY | NOHEATCLOUD;
		explode wing1 type BITMAPONLY | NOHEATCLOUD;
		explode thrusta type BITMAPONLY | NOHEATCLOUD;
		explode thrustb type BITMAPONLY | NOHEATCLOUD;
		return (corpsetype);
	}
	corpsetype = 3;
	explode base type BITMAPONLY | NOHEATCLOUD;
	explode flare1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP2 | NOHEATCLOUD;
	explode thrusta type BITMAPONLY | NOHEATCLOUD;
	explode thrustb type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3 | NOHEATCLOUD;
	return corpsetype;
}
