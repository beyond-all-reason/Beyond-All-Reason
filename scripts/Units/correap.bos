
#include "../recoil_common_includes.h"

piece  tracks, base, flare1, flare2, turret, base, sleeves, barrel1, barrel2, dirt1, dirt2, exhaust1, exhaust2, exhaust3, exhaust4;

static-var  restore_delay, gun_1;

// Signal definitions
#define SIGNAL_AIM1 256
#define SIGNAL_MOVE 1

#define VD_PIECE1 dirt1
#define VD_PIECE2 dirt2
#define VD_AMPHIBIOUS
#define VD_DIRTCEG 1024 + 1
#include "../vehicle_dirt_cegs.h"

#define BASEPIECE base
#define HITSPEED <45.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 7
#define MAXTILT 100
#define RECOIL_POWER 90000
#include "../unit_hitbyweaponid_and_smoke.h"

#define TB_BASE base 
#define TB_TURNRATE <15.0> 
#define TB_TILT_X <0.15>
#define TB_BANK_Z <0.10> // Do not define this if you dont want banking
#include "../tilt_bank_mobileunit.h"

Create()
{
	hide dirt1;
	hide dirt2;
	hide flare1;
	hide flare2;
	hide exhaust1;
	hide exhaust2;
	hide exhaust3;
	hide exhaust4;
	gun_1 = 0;
	restore_delay = 3000;
	call-script TB_Init();
}

StartMoving(reversing)
{
	signal SIGNAL_MOVE;
	set-signal-mask SIGNAL_MOVE;
	start-script Vehicle_Dirt_Ceg();
	START_TILTBANK;
}


StopMoving()
{
	signal SIGNAL_MOVE;
	STOP_TILTBANK;
}

SetMaxReloadTime(reloadMS)
{
	restore_delay = reloadMS * 2;
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	turn turret to y-axis <0.0> speed <35.005495>;
	turn sleeves to x-axis <0.0> speed <15.0>;
}
SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

AimWeapon1(heading, pitch)
{
	signal SIGNAL_AIM1;
	set-signal-mask SIGNAL_AIM1;
	turn turret to y-axis heading speed <85.005495>;
	turn sleeves to x-axis <0.0> - pitch speed <50.005495>;
	wait-for-turn turret around y-axis;
	wait-for-turn sleeves around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FireWeapon1()
{
	if( gun_1 == 0 )
	{
		move barrel1 to z-axis [-2.400000] speed [500.0];
		emit-sfx 1024 + 0 from flare1;
		sleep 100;
		wait-for-move barrel1 along z-axis;
		move barrel1 to z-axis [0.0] speed [3.0];
	}
	else
	{
		move barrel2 to z-axis [-2.400000] speed [500.0];
		emit-sfx 1024 + 0 from flare2;
		sleep 100;
		wait-for-move barrel2 along z-axis;
		move barrel2 to z-axis [0.0] speed [3.0];
	}
	gun_1 = !gun_1;
}

QueryWeapon1(pieceIndex)
{
	pieceIndex =  flare1 + gun_1;
}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = sleeves;
}



Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret type BITMAPONLY | NOHEATCLOUD;
		explode sleeves type BITMAPONLY | NOHEATCLOUD;
		explode barrel1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret type FALL | NOHEATCLOUD;
		explode sleeves type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrel1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode sleeves type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrel1 type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode sleeves type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrel1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
