
#include "../recoil_common_includes.h"

piece  flarea, barrelb1, barrelb2, barrelb3, base, guna, turreta, turretb,
       flareb1, flareb2, flareb3, wake, bow, sleeveb1, sleeveb2, sleeveb3, sleevea, barrela1, barrela2, barrela3;

static-var  gun_2, restore_delay;
static-var reloading_barrel_1, reloading_barrel_2;
static-var deltaheading, newchassisheading, chassisheading, restore_position, aimDir1;
// Signal definitions
#define SIGNAL_AIM1	256
#define SIGNAL_AIM2 512
#define SIGNAL_AIM3 1024
#define SIGNAL_AIM4 2048
#define SIGNAL_MOVE 1

#define RB_MASS 40
#define RB_LENGTH 10
#define RB_WIDTH 4

#define RB_PITCH_SPEED 200
#define RB_PITCH_ACCELERATION 10
#define RB_ROLL_ACCELERATION 16
#define RB_RECOIL_ENERGY_1 150
#define RB_RECOIL_ENERGY_2 150
#define RB_BOWSPLASH_PIECE bow

#include "../bar_ships_common.h"



//-------------------------------CONSTANT ACCELERATION TURRET TURNING---------------------------
// MaxVelocity and acceleration are in degrees per frame (not second!)
// Jerk is the minimum velocity of the turret
// A high precision requirement can result in overshoots if desired 
// Author Beherith mysterme@gmail.com. License: GNU GPL v2.
// adjustments by Itanthias
#define MAX_AIMY1_VELOCITY <2.20>
#define AIMY1_ACCELERATION <0.13>
#define AIMY1_SNAP_TOLERANCE <0.5>
#define AIMY1_PRECISION <1.2>
#define AIMY1_RESTORE_SPEED <1.0>

static-var aimy1delta, timetozero, deceleratethreshold;
static-var aimy1velocity, aimy1target, aimy1position, gameFrame;
static-var  Stunned;

ExecuteRestoreAfterDelay()
{
    while (Stunned) {
		sleep 1;
    }

	restore_position = 1;
	turn sleeveb1 to x-axis <0.0> speed <30.0>;
	turn sleeveb2 to x-axis <0.0> speed <30.0>;
	turn sleeveb3 to x-axis <0.0> speed <30.0>;
	
}
SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

AimControl()
{
	
	//-------------------------------CONSTANT ACCELERATION TURRET TURNING---------------------------
	while( TRUE  ){
		// only turn when not stunned
		if (Stunned == 0){

			// apply correction for chassis turning
			newchassisheading = get HEADING; //get heading
			deltaheading = newchassisheading - chassisheading;
			chassisheading = newchassisheading;
			aimy1target = aimy1target - deltaheading;

			// apply aimy1target for neutral, restored, position
			if (restore_position == 1)
			{
				aimy1target = 0;
			}
	
			aimy1delta = aimy1target - aimy1position ; 	
			//Clamp angles between <-180>;<180>
			while (aimy1target >  <180>) aimy1target = aimy1target - <360>;
			while (aimy1target < <-180>) aimy1target = aimy1target + <360>;
			while (aimy1position >  <180>) aimy1position = aimy1position - <360>;
			while (aimy1position < <-180>) aimy1position = aimy1position + <360>;
			while (aimy1delta >  <180>) aimy1delta = aimy1delta - <360>;
			while (aimy1delta < <-180>) aimy1delta = aimy1delta + <360>;
			
			// Snap turret exactly to aimy1target if within snap tolerance
			if (get ABS(aimy1delta) < AIMY1_SNAP_TOLERANCE) {
				aimy1position = aimy1target;
				turn turretb to y-axis aimy1position now;	

				// velocity match to chassis heading change
				if ( (restore_position == 0) ){
					aimy1velocity = (-1)*deltaheading;
					// clamp velocity to max value
					aimy1velocity = get MIN(       MAX_AIMY1_VELOCITY, aimy1velocity); 
					aimy1velocity = get MAX((-1) * MAX_AIMY1_VELOCITY, aimy1velocity);
				}
				else
				{
					aimy1velocity = 0;
				}
			}
			else
			{

				//number of frames required to decelerate to 0
				//account for chassis rotation speed
				timetozero = get ABS(aimy1velocity - deltaheading) / AIMY1_ACCELERATION;
			
				//distance from target where we should start decelerating, always 'positive'
				//pos = t * v - (t*(t-1)*a/2)
				deceleratethreshold = timetozero * (get ABS(aimy1velocity)) - (timetozero * (timetozero - 1) * AIMY1_ACCELERATION / 2); 
			
				if (get ABS(aimy1delta) <= deceleratethreshold){ //we need to decelerate
					if (aimy1velocity > 0) aimy1velocity = aimy1velocity - AIMY1_ACCELERATION;
					else 				   aimy1velocity = aimy1velocity + AIMY1_ACCELERATION;
			
				}	
				else //we need to accelerate
				{
					if (aimy1delta > 0) aimy1velocity = get MIN(       MAX_AIMY1_VELOCITY, aimy1velocity + AIMY1_ACCELERATION); 
					else                aimy1velocity = get MAX((-1) * MAX_AIMY1_VELOCITY, aimy1velocity - AIMY1_ACCELERATION);
				}

				if (get ABS(aimy1velocity) < (2 * AIMY1_SNAP_TOLERANCE) ){
					if (get ABS(aimy1delta) < AIMY1_SNAP_TOLERANCE) aimy1velocity = aimy1delta;
					//if ((aimy1delta >        AIMY1_JERK)) aimy1velocity =        AIMY1_JERK;
					//if ((aimy1delta < (-1) * AIMY1_JERK)) aimy1velocity = (-1) * AIMY1_JERK;
				}
			
				aimy1position = aimy1position + aimy1velocity; 
				turn turretb to y-axis aimy1position now;
			}

		}
		sleep 30;
	}
}

Create()
{
	hide bow;
	hide flareb3;
	hide flareb1;
	hide flareb2;
	hide flarea;
	hide wake;
	hide flarea;
	hide flareb1;
	hide flareb2;
	hide flareb3;
	gun_2 = 1;

	chassisheading = get HEADING;
	newchassisheading = chassisheading;
	deltaheading = 0;

	restore_position = 1;

	aimy1position = 0;
	aimy1target = 0;
	aimy1delta = 0;

	restore_delay = 3000;	
	start-script InitRockBoat();
	SLEEP_UNTIL_UNITFINISHED;
	start-script BoatPhysics();

	start-script AimControl();
}

SetMaxReloadTime(reloadMS)
{
	restore_delay = reloadMS * 3;
}

AimWeapon1(heading, pitch)
{

	signal SIGNAL_AIM1;
	set-signal-mask SIGNAL_AIM1;
	aimDir1 = heading;

	//We can do this any time
	restore_position = 0;
	turn sleeveb1 to x-axis RB_AIMPITCHCORRECTION(heading) - pitch speed <30.0>;
	turn sleeveb2 to x-axis RB_AIMPITCHCORRECTION(heading) - pitch speed <30.0>;
	turn sleeveb3 to x-axis RB_AIMPITCHCORRECTION(heading) - pitch speed <30.0>;
	start-script RestoreAfterDelay();

	// let AimControl know what to aim at
	aimy1target = heading;
	while ( TRUE )
	{
		// condition 1, is aim good?
		aimy1delta = aimy1target - aimy1position ; 	
		if (get ABS(aimy1delta) < AIMY1_PRECISION) {
			return (1); 
		}
		sleep 1;
	}
}

AimWeapon2(heading, pitch)
{
	signal SIGNAL_AIM2;
	set-signal-mask SIGNAL_AIM2;
	aimy1target = heading;
	while ( TRUE )
	{
		// condition 1, is aim good?
		aimy1delta = aimy1target - aimy1position ; 	
		if (get ABS(aimy1delta) < AIMY1_PRECISION) {
			// condition 2, is barrel 1 reloading?
			reloading_barrel_1 = (get GAME_FRAME) < (get WEAPON_RELOADSTATE(1));
			if (reloading_barrel_1) {
				return (1); 
			}
		}
		sleep 2;
	}
}

AimWeapon3(heading, pitch)
{
	signal SIGNAL_AIM3;
	set-signal-mask SIGNAL_AIM3;
	aimy1target = heading;
	while ( TRUE )
	{
		// condition 1, is aim good?
		aimy1delta = aimy1target - aimy1position; 	
		if (get ABS(aimy1delta) < AIMY1_PRECISION) {
			// condition 2, are barrels 1 and 2 reloading?
			reloading_barrel_1 = (get GAME_FRAME) < (get WEAPON_RELOADSTATE(1));
			reloading_barrel_2 = (get GAME_FRAME) < (get WEAPON_RELOADSTATE(2));
			if (reloading_barrel_1 AND reloading_barrel_2) {
				return (1);
			}
		}
		sleep 3;
	}
}

RestoreLaser()
{
	sleep restore_delay;
	//set-signal-mask 0;
	turn turreta to y-axis <0.0> speed <100.0>;
}



StartMoving(reversing)
{

}

StopMoving()
{

}

AimWeapon4(heading, pitch)
{
	signal SIGNAL_AIM4;
	set-signal-mask SIGNAL_AIM4;
	turn turreta to y-axis heading speed <200.0>;
	wait-for-turn turreta around y-axis;
	start-script RestoreLaser();
	return (1);
}

FireWeapon4()
{
	if( gun_2 == 0 )
	{
		sleep 250;
		turn guna to z-axis <0.0> speed <400.0>;
		wait-for-turn guna around z-axis;
	}
	else if( gun_2 == 1 )
	{
		sleep 250;
		turn guna to z-axis <120.027473> speed <400.0>;
		wait-for-turn guna around z-axis;
	}
	else if( gun_2 == 2 )
	{
		sleep 250;
		turn guna to z-axis <-120.027473> speed <400.0>;
		wait-for-turn guna around z-axis;
	}
	gun_2 = (gun_2 + 1 ) % 3;
}

FireWeapon1()
{
	//delays other barrels from firing for 15 frames
	get WEAPON_RELOADSTATE(-2,get MAX(get GAME_FRAME + 15,get WEAPON_RELOADSTATE(2)));
	get WEAPON_RELOADSTATE(-3,get MAX(get GAME_FRAME + 15,get WEAPON_RELOADSTATE(3)));

	emit-sfx 1024 + 0 from flareb1;
	move barrelb1 to z-axis [-5.0] now; // [-2.500000]
	sleep 100;
	move barrelb1 to z-axis [0.0] speed [2.0];
	RB_RECOILBOAT(aimDir1, RB_RECOIL_ENERGY_1);

}

FireWeapon2()
{
	//delays other barrels from firing for 15 frames
	get WEAPON_RELOADSTATE(-1,get MAX(get GAME_FRAME + 15,get WEAPON_RELOADSTATE(1)));
	get WEAPON_RELOADSTATE(-3,get MAX(get GAME_FRAME + 15,get WEAPON_RELOADSTATE(3)));

	emit-sfx 1024 + 0 from flareb2;
	move barrelb2 to z-axis [-5.0] now;
	sleep 100;
	move barrelb2 to z-axis [0.0] speed [2.0];
	RB_RECOILBOAT(aimDir1, RB_RECOIL_ENERGY_1);

}

FireWeapon3()
{
	//delays other barrels from firing for 15 frames
	get WEAPON_RELOADSTATE(-1,get MAX(get GAME_FRAME + 15,get WEAPON_RELOADSTATE(1)));
	get WEAPON_RELOADSTATE(-2,get MAX(get GAME_FRAME + 15,get WEAPON_RELOADSTATE(2)));

	emit-sfx 1024 + 0 from flareb3;
	move barrelb3 to z-axis [-5.0] now;
	sleep 100;
	move barrelb3 to z-axis [0.0] speed [2.0];
	RB_RECOILBOAT(aimDir1, RB_RECOIL_ENERGY_1);

}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = turretb;
}

QueryWeapon1(pieceIndex)
{
	pieceIndex = flareb1;
}

AimFromWeapon2(pieceIndex)
{
	pieceIndex = turretb;
}

QueryWeapon2(pieceIndex)
{
	pieceIndex = flareb2;
}

AimFromWeapon3(pieceIndex)
{
	pieceIndex = turretb;
}

QueryWeapon3(pieceIndex)
{
	pieceIndex = flareb3;
}

AimFromWeapon4(pieceIndex)
{
	pieceIndex = turreta;
}

QueryWeapon4(pieceIndex)
{
	pieceIndex = flarea;
}



Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turreta type BITMAPONLY | NOHEATCLOUD;
		explode sleevea type BITMAPONLY | NOHEATCLOUD;
		explode guna type BITMAPONLY | NOHEATCLOUD;
		explode barrela3 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrela2 type BITMAPONLY | NOHEATCLOUD;
		explode barrela1 type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turreta type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode sleevea type FALL | NOHEATCLOUD;
		explode guna type FALL | NOHEATCLOUD;
		explode barrela3 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrela2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrela1 type FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turreta type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode sleevea type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode guna type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrela3 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrela2 type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode barrela1 type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turreta type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode sleevea type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode guna type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrela3 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrela2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrela1 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
	return corpsetype;
}
