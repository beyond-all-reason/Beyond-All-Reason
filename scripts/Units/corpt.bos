
#include "../recoil_common_includes.h"

piece base, turret, launcher, radar, wake1, flare1, flare2, flare3, blink;

static-var  gun_1, restore_delay;

// Signal definitions
#define SIGNAL_MOVE 1
#define SIGNAL_AIM1 256
#define SIGNAL_AIM3 1024

#define BASEPIECE base
#define HITSPEED <20.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 2
#define MAXTILT 200

#include "../unit_hitbyweaponid_and_smoke.h"

Lights()
{
	while( TRUE )
	{
		emit-sfx 1024 + 1 from blink;
		sleep 2500;
	}
	sleep 50;
	start-script Lights();
}

Go()
{
	spin radar around x-axis speed <-50.043956> accelerate <2>;
	return (0);
}

Stop()
{
	spin radar around x-axis speed <0.0>;
	return (0);
}

Create()
{
	hide flare1;
	hide flare2;
	hide flare3;
	hide wake1;
	gun_1 = 1;
	restore_delay = 3000;
	start-script Lights();
	return (0);
}

Activate()
{
	start-script Go();
}

Deactivate()
{
	start-script Stop();
}

SetMaxReloadTime(reloadMS)
{
	restore_delay = reloadMS * 2;
	return (0);
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	set-signal-mask 0;
	turn turret to y-axis <0.0> speed <150.0>;
	turn turret to x-axis <0.0> speed <150.0>;
}
SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

StartMoving(reversing)
{
	signal SIGNAL_MOVE;
	set-signal-mask SIGNAL_MOVE;
	var waketime;
	while( TRUE )
	{
		emit-sfx 1024 + 0 from wake1;
		waketime = (get CURRENT_SPEED) * 100 / get MAX_SPEED;
		if( waketime < 50 ) waketime = 50;
		sleep 10000 / waketime;
	}
}

StopMoving()
{
	signal SIGNAL_MOVE;
	return (0);
}


AimWeapon1(heading, pitch)
{
	signal SIGNAL_AIM1;
	set-signal-mask SIGNAL_AIM1;
	turn turret to y-axis heading speed <300.0>;
	turn launcher to x-axis <0> - pitch speed <300>;  //?
	wait-for-turn turret around y-axis;
	wait-for-turn launcher around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FireWeapon1()
{
	++gun_1;
	if( gun_1 == 4 )
	{
		gun_1 = 1;
	}
	return (0);
}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = launcher;
	return (0);
}

QueryWeapon1(pieceIndex)
{
	if( gun_1 == 1 )
	{
		pieceIndex = flare1;
	}
	if( gun_1 == 2 )
	{
		pieceIndex = flare2;
	}
	if( gun_1 == 3 )
	{
		pieceIndex = flare3;
	}
	return (0);
}



Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode launcher type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode radar type BITMAPONLY | NOHEATCLOUD;
		explode turret type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode launcher type FALL | NOHEATCLOUD;
		explode radar type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode launcher type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode radar type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode launcher type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode radar type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
	return corpsetype;
}
