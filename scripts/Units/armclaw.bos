
#include "../recoil_common_includes.h"

piece  base, post, turret, barrel, flare, aimpoint, sparkemit, glow;

static-var  isOpen, restore_delay;

// Signal definitions
#define SIGNAL_AIM1 256

EmitSparks()
{
	show sparkemit;
	sleep 200;
	hide sparkemit;
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	start-script Deactivate();
}
SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

Create()
{
	hide aimpoint;
	hide flare;
	hide sparkemit;

	restore_delay = 2000;
	isOpen = 0;
	start-script Activate();
	
	move aimpoint to y-axis [5] speed [150.0];
	SLEEP_UNTIL_UNITFINISHED;
	start-script RestoreAfterDelay();
}


#define BASEPIECE base
#define MAXTILT 0
#include "../unit_hitbyweaponid_and_smoke.h"

Activate()
{
	move post to y-axis [4] speed [150.0];
	move barrel to z-axis [0.0] speed [90.0];
	wait-for-move post along y-axis;
	wait-for-move barrel along z-axis;
	show glow;
	isOpen = 1;
	set ARMORED to 0;
}

Deactivate()
{
	set ARMORED to 1;
	isOpen = 0;
	turn turret to x-axis <0.0> speed <300.0>;
	turn post to y-axis <0.0> speed <300.0>;
	move barrel to z-axis [-8.0] speed [20.0];
	wait-for-turn barrel around x-axis;
	wait-for-turn post around y-axis;
	hide glow;
	move post to y-axis [-26.0] speed [90.0];
	wait-for-move post along y-axis;
}

SetMaxReloadTime(reloadMS)
{
	restore_delay = reloadMS * 2;
}

AimWeapon1(heading, pitch)
{
	if (!isOpen) start-script Activate();
	signal SIGNAL_AIM1;
	set-signal-mask SIGNAL_AIM1;
	while( !isOpen )
	{
		sleep 250;
	}
	turn post to y-axis heading speed <300.0>;
	if (pitch < 0) pitch = 0;
	turn turret to x-axis <0.0> - pitch speed <300.0>;
	wait-for-turn post around y-axis;
	wait-for-turn turret around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FireWeapon1()
{
	emit-sfx 1024+0 from flare;
	call-script EmitSparks();
	sleep 150;
}

QueryWeapon1(pieceIndex)
{
	pieceIndex = flare;
}



AimFromWeapon1(pieceIndex)
{
	pieceIndex = aimpoint;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode post type BITMAPONLY | NOHEATCLOUD;
		explode turret type BITMAPONLY | NOHEATCLOUD;
		explode barrel type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare type BITMAPONLY | NOHEATCLOUD;
		explode base type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode post type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrel type FALL | NOHEATCLOUD;
		explode flare type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode post type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type SMOKE | FALL | NOHEATCLOUD;
		explode barrel type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare type SMOKE | FALL | NOHEATCLOUD;
		explode base type SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode post type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode barrel type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
