#include "../recoil_common_includes.h"

piece  base, armor, cockpit, engine, hatchpivot, hatch, torpedo, thrust1, thrust2, thrust3, flare;

static-var  restore_delay;
// Signal definitions
#define SIGNAL_AIM1 256
#define SIGNAL_MOVE 1

#define BASEPIECE base
#define HITSPEED <25.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 5
#define MAXTILT 200

#include "../unit_hitbyweaponid_and_smoke.h"

#define TB_BASE base 
#define TB_TURNRATE <30.0> 
#define TB_TILT_X <-0.32>
#define TB_BANK_Z <0.5> // Do not define this if you dont want banking
#define TB_WAKE_PIECE base
#define TB_WAKE_FOAM 1024 + 1
#include "../tilt_bank_submarine.h"

Create()
{
	hide flare;
	turn hatchpivot to z-axis <-30> now;
	restore_delay = 3000;
	call-script TB_Init();
	return (0);
}

StartMoving(reversing)
{
	signal SIGNAL_MOVE;
	set-signal-mask SIGNAL_MOVE;
	START_TILTBANK;
	
	show thrust1;
	show thrust2;
	show thrust3;
}

StopMoving()
{
	signal SIGNAL_MOVE;
	STOP_TILTBANK;
	
	hide thrust1;
	hide thrust2;
	hide thrust3;
}

FireWeapon1()
{
	emit-sfx 1024 + 2 from flare;
	move torpedo to z-axis [-12] now;
	sleep 2000;
	move torpedo to z-axis [0] speed [5];
	return (0);
}

QueryWeapon1(pieceIndex)
{
	pieceIndex = flare;
	return (0);
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
    turn hatch to y-axis <0> speed <30>;
	wait-for-turn hatch around y-axis;
}

SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = base;
}

AimWeapon1(heading, pitch)
{
	signal SIGNAL_AIM1;
	set-signal-mask SIGNAL_AIM1;
	
    turn hatch to y-axis <-135> speed <360>;
	wait-for-turn hatch around y-axis;
	
	start-script RestoreAfterDelay();
	return (1);
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode engine type BITMAPONLY | NOHEATCLOUD;
		explode cockpit type FIRE | SMOKE | NOHEATCLOUD;
		explode armor type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode engine type BITMAPONLY | NOHEATCLOUD;
		explode cockpit type FIRE | SMOKE | NOHEATCLOUD;
		explode armor type NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode engine type FIRE | SMOKE | NOHEATCLOUD;
		explode cockpit type FIRE | SMOKE | NOHEATCLOUD;
		explode armor type FIRE | SMOKE | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode engine type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode cockpit type EXPLODE_ON_HIT | FIRE | NOHEATCLOUD;
		explode armor type EXPLODE_ON_HIT | FIRE | NOHEATCLOUD;
	return corpsetype;
}
