
#include "../recoil_common_includes.h"

piece  ground, base, anti, antiflare, door1, door2, front, radar, blink, wake1, wake2;

static-var  oldHead;
static-var  Stunned;

// Signal definitions
#define SIGNAL_MOVE 1

#define SIGNAL_AIM2 512


#define RB_MASS 40
#define RB_LENGTH 8
#define RB_WIDTH 3
#define RB_PITCH_ACCELERATION 10
#define RB_ROLL_ACCELERATION 8
#define RB_WAKE_PIECE wake1
#define RB_WAKE_CEG 1024 + 1
#define RB_BOWSPLASH_PIECE wake2
#define RB_BOWSPLASH_CEG 1024 + 2

#include "../bar_ships_common.h"

Lights()
{
	while( TRUE )
	{
	    if (!Stunned) {
		    emit-sfx 1024 + 0 from blink;
		}
		sleep 2500;
	}
}

ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	set-signal-mask 0;
	turn door1 to x-axis <0> speed <70>;
	turn door2 to x-axis <0> speed <70>;
	wait-for-turn door1 around x-axis;
}
SetStunned(State)
{
    Stunned = State;
	if (Stunned) {
	    call-script Activate();
	} else {
	    call-script Deactivate();
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep 3000;
	start-script ExecuteRestoreAfterDelay();
}


Create()
{
	hide wake1;
	hide wake2;
	hide blink;
	hide antiflare;
	hide ground;
	start-script InitRockBoat();
	SLEEP_UNTIL_UNITFINISHED;
	start-script BoatPhysics();
	start-script Activate();
}

Activate()
{
	start-script Lights();
	spin radar around y-axis speed <-50.043956> accelerate <2>;
}

Deactivate()
{
	spin radar around y-axis speed <0.0>;
}


StartMoving(reversing)
{
}

StopMoving()
{
}

AimWeapon1(heading, pitch)
{

	signal SIGNAL_AIM2;
	set-signal-mask SIGNAL_AIM2;
	turn door1 to x-axis <40> speed <70>;
	turn door2 to x-axis <-40> speed <70>;
	wait-for-turn door1 around x-axis;
	start-script RestoreAfterDelay();
	return(1);
}

FireWeapon1()
{
}

QueryWeapon1(pieceIndex)
{
	pieceIndex = antiflare;
}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = antiflare;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode ground type BITMAPONLY | NOHEATCLOUD;
		explode base type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode ground type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode ground type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode base type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode ground type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode base type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
	return corpsetype;
}
