#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, cover1, cover2;

static-var  mmState, Active;

activatescr()
{
	turn cover1 to y-axis <-45> speed <60>;
	turn cover2 to y-axis <45> speed <60>;
	sleep 1603;
}

deactivatescr()
{
	turn cover1 to y-axis <0> speed <60>;
	turn cover2 to y-axis <0> speed <60>;
	sleep 1603;
}

RockOnWaves()
{
	var rockDir, rockAng, rockSpeed;
	rockDir = <-0.1>;
	rockAng = <-0.1>;
	while( TRUE )
	{
		while( get ABS(rockAng) < <1.31> )
		{
			rockSpeed = <1.45> - get ABS(rockAng);
			turn base to x-axis rockAng speed rockSpeed*5;
			turn base to z-axis rockAng speed rockSpeed*5;
			wait-for-turn base around x-axis;
			wait-for-turn base around z-axis;
			rockAng = rockAng + rockDir;
		}
		rockDir = 0 - rockDir;
		rockAng = rockAng + rockDir + rockDir;
	}
}

Go()
{
	start-script activatescr();
	dont-cache cover1;
}

Stop()
{
	call-script deactivatescr();
	cache cover1;
}

ImAMetalMaker()
{
	mmState = -1;
	while( TRUE )
	{
		if (Active == 1)
		{
			if (mmState != 1) {
				call-script Go();
				set ACTIVATION to 1;
				mmState = 1;
			}
		}
		else
		{
			if (mmState != 0) {
				call-script Stop();
				set ACTIVATION to 0;
				mmState = 0;
			}
		}
		sleep 1000;
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	start-script RockOnWaves();
	start-script ImAMetalMaker();
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

MMStatus(State)
{
	Active = State;
	//get PRINT(Active);
}

Create()
{
	dont-cache cover1;
	dont-shade cover1;
	start-script SmokeUnit();
}


SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode cover1 type FIRE | SMOKE | FALL | BITMAPONLY | NOHEATCLOUD;
		explode cover2 type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode cover1 type FIRE | FALL | NOHEATCLOUD;
		explode cover2 type SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode cover1 type FIRE | SMOKE | FALL | BITMAPONLY | NOHEATCLOUD;
		explode cover2 type FIRE | SMOKE | FALL | BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode cover1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode cover2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
	return corpsetype;
}
