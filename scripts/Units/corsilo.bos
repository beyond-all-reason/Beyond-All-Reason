


#include "../recoil_common_includes.h"

piece  base, door1, door2, flare, cagelight1, cagelight2, cagelight3, cagelight4, cagelight_emit1, cagelight_emit2, cagelight_emit3, cagelight_emit4;

static-var  restore_delay, shotcount;

static-var  silo_state;
// 0 = closed
// 1 = opening
// 2 = opened
// 3 = closing, fired missile
// 4 = closing, no missile fired

#define BASEPIECE base
#define MAXTILT 0
#include "../unit_hitbyweaponid_and_smoke.h"

// Signal definitions
#define SIGNAL_AIM1 256

Create()
{
	hide flare;
	hide cagelight_emit1;
	hide cagelight_emit2;
	hide cagelight_emit3;
	hide cagelight_emit4;
	shotcount = 0;
	restore_delay = 15000;
	silo_state = 0;
	return (0);
}

lua_UnitScriptLight(lightIndex, count) 
{
	return 0;
}

OpenSilo()
{
	if ( (silo_state==0) OR (silo_state==4) )
	{
		// the sound effect
		emit-sfx 4096 + 1 from flare;
	}
	if ( NOT (silo_state==3) )
	{
		// 8.5 seconds to open up
		silo_state = 1;
		spin cagelight1 around y-axis speed <420>;
		spin cagelight2 around y-axis speed <420>;
		spin cagelight3 around y-axis speed <420>;
		spin cagelight4 around y-axis speed <420>;
		show cagelight_emit1;
		show cagelight_emit2;
		show cagelight_emit3;
		show cagelight_emit4;
		turn door1 to z-axis <127.5> speed <15>;
		turn door2 to z-axis <-127.5> speed <15>;
		wait-for-turn door1 around z-axis;
		wait-for-turn door2 around z-axis;
		silo_state = 2;
	}
}

CloseSiloDoors()
{
	// 30 seconds to close from <127.5> open position
	stop-spin cagelight1 around y-axis;
	stop-spin cagelight2 around y-axis;
	stop-spin cagelight3 around y-axis;
	stop-spin cagelight4 around y-axis;
	hide cagelight_emit1;
	hide cagelight_emit2;
	hide cagelight_emit3;
	hide cagelight_emit4;

	turn door1 to z-axis <0> speed <4.25>;
	turn door2 to z-axis <0> speed <4.25>;
	wait-for-turn door1 around z-axis;
	wait-for-turn door2 around z-axis;
	silo_state = 0;
}

CloseSilo_Fired()
{
	silo_state = 3;
	signal SIGNAL_AIM1; // kill any OpenSilo threads
	start-script CloseSiloDoors();
}

RestoreAfterDelay()
{
	sleep restore_delay;
	if ( NOT (silo_state==3) )
	{
		silo_state = 4;
		start-script CloseSiloDoors();
	}
	return (0);
}

AimWeapon1(heading, pitch)
{
	signal SIGNAL_AIM1;
	set-signal-mask SIGNAL_AIM1;
	start-script OpenSilo();
	start-script RestoreAfterDelay();
	if (silo_state == 2)
	{
		return (1);
	}
	return (0);
}

FireWeapon1()
{
	shotcount = shotcount + 1;
	call-script lua_UnitScriptLight(1, shotcount);
	call-script lua_UnitScriptLight(2, shotcount);
	start-script CloseSilo_Fired();
	return (0);
}

QueryWeapon1(pieceIndex)
{
	pieceIndex = flare;
	return (0);
}



Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode door1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode door2 type BITMAPONLY | NOHEATCLOUD;
		explode flare type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode door1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode door2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode door1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode door2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare type SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode door1 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode door2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode flare type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
