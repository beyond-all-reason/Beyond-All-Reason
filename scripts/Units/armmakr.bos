#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, cover1, cover2;

static-var  mmState, Active;

ImAMetalMaker()
{
	mmState = -1;
	while( TRUE )
	{	
		if (Active == 1)
		{
			turn cover1 to z-axis <-60> speed <60>;
			turn cover2 to z-axis <60> speed <60>;
			if (mmState != 1) {
				set ACTIVATION to 1;
				mmState = 1;
			}
		}
		else
		{
		
			turn cover1 to z-axis <0> speed <60>;
			turn cover2 to z-axis <0> speed <60>;
			if (mmState != 0) {
				set ACTIVATION to 0;
				mmState = 0;
			}
		}
		sleep 1000;
	}
}


SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 500;
	}
	start-script ImAMetalMaker();
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 258;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 257;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

MMStatus(State)
{
	Active = State;
	//get PRINT(Active);
}

Create()
{
	start-script SmokeUnit();
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode cover1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode cover2 type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode cover1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode cover2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode cover1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode cover2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode cover1 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode cover2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
