
#include "../recoil_common_includes.h"	
#include "../factories_common.h"

piece  base, pad, head1, head2, nano1, nano2, nano3, nano4, center1, center2, side1, side2, mount1, mount2, cagelight, cagelight_emit, cagelight2, cagelight_emit2;

static-var  spray;

// Signal definitions
#define SIGNAL_BUILD 2
#define SIGNAL_TURNON 4


Create()
{
	hide nano2;
	hide nano1;
	hide nano3;
	hide nano4;
	hide cagelight_emit;
	hide cagelight_emit2;
	spray = 0;
}


#define BASEPIECE base
#define MAXTILT 0
#include "../unit_hitbyweaponid_and_smoke.h"

QueryNanoPiece(pieceIndex)
{
	spray = (spray + 1) % 4;
	pieceIndex = nano1 + spray;
}

Activate()
{
	signal SIGNAL_TURNON;
	set-signal-mask SIGNAL_TURNON;
	
	move side1 to z-axis [24] speed [24];
	wait-for-move side1 along z-axis;
	
	move side2 to z-axis [10] speed [5];
	wait-for-move side2 along z-axis;

	FACTORY_OPEN_BUILD;
}

Deactivate()
{
	signal SIGNAL_TURNON;
	set-signal-mask SIGNAL_TURNON;
	sleep 5000;
	
	move side1 to z-axis [0] speed [24];
	wait-for-move side1 along z-axis;
	
	move side2 to z-axis [0] speed [5];
	wait-for-move side2 along z-axis;
	
	FACTORY_CLOSE_BUILD;
}

Movecenters(){
	var angle;
	
	set-signal-mask SIGNAL_BUILD;
	while(TRUE){
		angle = RAND(0,48);
		if ((angle %2) == 0){
			move mount1 to z-axis [1] * angle speed [24];
			turn head1 to y-axis <-1.6> * angle speed <36>;
			wait-for-move mount1 along z-axis;
		}
		else
		{
			move mount2 to z-axis [1] * angle speed [24];
			turn head2 to y-axis <-1.6> * angle speed <36>;
			wait-for-move mount2 along z-axis;
		}
	}
}


StartBuilding()
{
	show nano4;
	show nano3;
	show nano2;
	show nano1;
	show cagelight_emit;
	show cagelight_emit2;
	spin cagelight  around y-axis speed <200> accelerate <1>;
	spin cagelight2 around y-axis speed <200> accelerate <1>;
	signal SIGNAL_BUILD;
	set-signal-mask SIGNAL_BUILD;
	start-script Movecenters();
}

StopBuilding()
{
	hide nano2;
	hide nano1;
	hide nano3;
	hide nano4;
	hide cagelight_emit;
	hide cagelight_emit2;
	stop-spin cagelight  around y-axis decelerate <1>;
	stop-spin cagelight2 around y-axis decelerate <1>;
	signal SIGNAL_BUILD;
	
	move mount1 to z-axis [0] speed [24];
	move mount2 to z-axis [0] speed [24];
	turn head1 to y-axis <0> speed <36>;
	turn head2 to y-axis <0> speed <36>;
	
	move center1 to z-axis [0] now;
	move center1 to z-axis [10] speed [20];
	move center2 to z-axis [10] now;
	
	wait-for-move center1 along z-axis;
	
	move center2 to z-axis [10] speed [20];
	move center1 to z-axis [0] speed [20];
	
	wait-for-move center2 along z-axis;
	
	move center2 to z-axis [0] speed [20];
	
	wait-for-move center2 along z-axis;
	 
	
}

QueryBuildInfo(pieceIndex)
{
	pieceIndex = pad;
}



Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode center1 type BITMAPONLY | NOHEATCLOUD;
		explode center2 type BITMAPONLY | NOHEATCLOUD;
		explode nano1 type BITMAPONLY | NOHEATCLOUD;
		explode nano2 type BITMAPONLY | NOHEATCLOUD;
		explode mount1 type BITMAPONLY | NOHEATCLOUD;
		explode mount2 type BITMAPONLY | NOHEATCLOUD;
		explode pad type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode center1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode center2 type FALL | NOHEATCLOUD;
		explode nano1 type FALL | NOHEATCLOUD;
		explode nano2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode mount1 type FALL | NOHEATCLOUD;
		explode mount2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode pad type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode center1 type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode center2 type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode nano1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode nano2 type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode mount1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode mount2 type SMOKE | FALL | NOHEATCLOUD;
		explode pad type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode center1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode center2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode nano1 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode nano2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode mount1 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode mount2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode pad type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
