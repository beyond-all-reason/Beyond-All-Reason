
#include "../recoil_common_includes.h"

piece  base, btrack, fltrack, frtrack, aimpoint, armor, turret, sleeve, lrail, rrail, flare;

static-var  restore_delay, wpn1_lasthead, shotcount, fired1, fired2, aiming, oldheading;

// Signal definitions
#define SIGNAL_MOVE 1
#define SIGNAL_AIM1	2
#define SIGNAL_AIM2	2
#define SIGNAL_IDLE 8 
#define SIGNAL_SHOOT1 16

#define BASEPIECE base
#define HITSPEED <45.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 5
#define MAXTILT 250
#define RECOIL_POWER 90000
#include "../unit_hitbyweaponid_and_smoke.h"

#define TB_BASE base 
#define TB_TURNRATE <15.0> 
#define TB_TILT_X <0.15>
#define TB_BANK_Z <0.10> // Do not define this if you dont want banking
#include "../tilt_bank_mobileunit.h"

Create()
{
	//hide flare;
	restore_delay = 3000;
	shotcount = 0;
	wpn1_lasthead = 1000000;
	fired1 = 0;
	fired2 = 0;
	aiming = 0;
	oldheading = get HEADING;
	call-script TB_Init();
	start-script Steering();
	SLEEP_UNTIL_UNITFINISHED;
}

Steering(heading, steery, currentSpeed)
{
	while(1)
	{
		heading = get HEADING;
		steery = (heading - oldheading)*2;
		
		turn fltrack to y-axis steery speed <120>;
		turn frtrack to y-axis steery speed <120>;

		sleep 66;
		oldheading = heading;
	}
}

// Some magic added by sethdgamre
ResetFire()
{
	signal SIGNAL_SHOOT1;
	set-signal-mask SIGNAL_SHOOT1;
	fired1 = 1;
	fired2 = 1;
	sleep 3000;
	fired2 = 0;
	sleep 3000;
	fired1 = 0;
}

ResetAiming()
{
	signal SIGNAL_IDLE;
	set-signal-mask SIGNAL_IDLE;
	aiming = 1;
	sleep 500;
	aiming = 0;
}

SetMaxReloadTime(Func_Var_1)
{
	restore_delay = Func_Var_1 * 2;
	return (0);
}
// End of Magic

StartMoving(reversing)
{
	signal SIGNAL_MOVE;
	set-signal-mask SIGNAL_MOVE;
	START_TILTBANK;
}

StopMoving()
{
	signal SIGNAL_MOVE;
	STOP_TILTBANK;
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	fired1 = 0;
	fired2 = 0;
	turn turret to y-axis <0> speed <50>;
	turn sleeve to x-axis <0> speed <50>;
	wpn1_lasthead = 1000000;
}

SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}

RestoreAfterDelay()
{
	set-signal-mask SIGNAL_AIM1;
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

AimPrimary(heading, pitch)
{
	signal SIGNAL_AIM1;
	set-signal-mask SIGNAL_AIM1;
	start-script ResetAiming();
	if (heading < <105> AND heading > <-105>){
		turn turret to y-axis heading speed <120>;
		turn sleeve to x-axis <0.000000> - pitch speed <120>;
		if (((get ABS(wpn1_lasthead - heading)) > 65536) OR(((get ABS(wpn1_lasthead - heading)) > <2>) AND ((get ABS(wpn1_lasthead - heading)) < 65536 - <2>)))
		{
			wpn1_lasthead = 1000000;
			wait-for-turn turret around y-axis;
			wait-for-turn sleeve around x-axis;
		}
		wpn1_lasthead = heading;
		start-script RestoreAfterDelay();
	}
	if(fired1 == 1)
	{
		return (0);
	}
	return (1);
}

AimSecondary(heading, pitch)
{
	signal SIGNAL_AIM2;
	set-signal-mask SIGNAL_AIM2;
	if(aiming == 1)
	{
		return (0);
	}
	turn turret to y-axis heading speed <120>;
	turn sleeve to x-axis <0.000000> - pitch speed <120>;
	if (((get ABS(wpn1_lasthead - heading)) > 65536) OR(((get ABS(wpn1_lasthead - heading)) > <2>) AND ((get ABS(wpn1_lasthead - heading)) < 65536 - <2>)))
	{
		wpn1_lasthead = 1000000;
		wait-for-turn turret around y-axis;
		wait-for-turn sleeve around x-axis;
	}
	wpn1_lasthead = heading;
	start-script RestoreAfterDelay();
	if(fired2 == 1)
	{
		return (0);
	}
	return (1);
}

FirePrimary()
{
	start-script ResetFire();
	++shotcount;
 	emit-sfx 1024 + 0 from flare;
	call-script lua_UnitScriptLight(1, shotcount);
	
 	move lrail to z-axis [-4] now;
 	move rrail to z-axis [-4] now;
 	move lrail to x-axis [-1] now;
 	move rrail to x-axis [1] now;
	sleep 300;
 	move lrail to z-axis [0] speed [2.5];
 	move rrail to z-axis [0] speed [2.5];
 	move lrail to x-axis [0] speed [2.5];
 	move rrail to x-axis [0] speed [2.5];
}

FireSecondary()
{
	start-script ResetFire();
	++shotcount;
 	emit-sfx 1024 + 0 from flare;
	call-script lua_UnitScriptLight(1, shotcount);
	
 	move lrail to z-axis [-6] now;
 	move rrail to z-axis [-6] now;
 	move lrail to x-axis [-1] now;
 	move rrail to x-axis [1] now;
	sleep 300;
 	move lrail to z-axis [0] speed [2.5];
 	move rrail to z-axis [0] speed [2.5];
 	move lrail to x-axis [0] speed [2.5];
 	move rrail to x-axis [0] speed [2.5];
}

lua_UnitScriptLight(lightIndex, count)
{
	return 0;
}

AimFromPrimary(piecenum)
{
	piecenum = aimpoint;
}

AimFromSecondary(piecenum)
{
	piecenum = aimpoint;
}

QueryPrimary(piecenum)
{
	piecenum = flare;
}

QuerySecondary(piecenum)
{
	piecenum = flare;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret type BITMAPONLY | NOHEATCLOUD;
		explode sleeve type BITMAPONLY | NOHEATCLOUD;
		explode rrail type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode sleeve type FALL | NOHEATCLOUD;
		explode rrail type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode sleeve type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rrail type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode sleeve type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode rrail type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
