
#include "../recoil_common_includes.h"


piece  base, flare, tube, rtube, ltube, wake, firing, firing1, firing2, prop, propr, propl;

static-var  restore_delay, shotcount, lastfired, silo_state, timeLeft, Stunned, is_firing, missiles_stockpiled;


// 0 = closed
// 1 = opening
// 2 = opened
// 3 = closing, fired missile
// 4 = closing, no missile fired

//I can't see a way around using state switches here? it has to know what it's doing / has just done

#define BASEPIECE base
#define HITSPEED <25.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 5
#define MAXTILT 200

//#include "../unit_hitbyweaponid_and_smoke.h"

#define TB_BASE base 
#define TB_TURNRATE <30.0> 
#define TB_TILT_X <-0.32>
#define TB_BANK_Z <0.5> // Do not define this if you dont want banking
#define TB_WAKE_PIECE base
#define TB_WAKE_FOAM 1024 + 1
#include "../tilt_bank_submarine.h"

// Signal definitions
#define SIGNAL_AIM1				2
#define SIGNAL_MOVE				4
#define SIGNAL_AIM2			8
#define SIGNAL_RESTORE			16


Create ()
{

	hide flare;
	hide firing;
	hide firing1;
	hide firing2;
	shotcount = 0;
	is_firing = 0;
	missiles_stockpiled = 0;

	restore_delay = 3000;
	timeLeft = restore_delay;//seems needed to avoid instant cancellation of first aiming anims
	call-script TB_Init();

	silo_state = 0;
	return (0);
}




SetStunned (State)
{
    Stunned = State;
}


lua_UnitScriptLight (lightIndex, count) 
{
	return 0;
}

OpenSilo()
{
    start-script TimeToRestore ();
    timeLeft = restore_delay;

	is_firing = 1;
	show firing;
	show firing1;
	show firing2;

	turn firing1 to y-axis <30> speed <2>;
	turn firing2 to y-axis <-30> speed <2>;
	spin firing around z-axis speed <70> accelerate <3>;
	if ( (silo_state==0) OR (silo_state==4) )
	{
		// the sound effect
		emit-sfx 4096 + 2 from flare;
	}
	if ( NOT (silo_state==3) )
	{
		// 8.5 seconds to open up (all timings wrong, ignore)
		silo_state = 1;

		turn tube to x-axis <-90> speed <15>;
		wait-for-turn tube around x-axis;

		silo_state = 2;
	}
}

CloseSiloDoors()
{
	is_firing = 0;
	turn firing1 to y-axis <0> speed <3>;
	turn firing2 to y-axis <0> speed <3>;
	stop-spin firing around z-axis decelerate 25;

	// 30 seconds to close from <127.5> open position

	turn tube to x-axis <0> speed <15>;
	wait-for-turn tube around x-axis;
	
	if (missiles_stockpiled==0) {
		hide firing;
		hide firing1;
		hide firing2;
	}


	silo_state = 0;
}

CloseSilo_Fired()
{
	silo_state = 3;
	signal SIGNAL_AIM2; // kill any OpenSilo threads
	start-script CloseSiloDoors();
}




TimeToRestore (addTime){
	signal SIGNAL_RESTORE;
	set-signal-mask SIGNAL_RESTORE;
    // This function may not be signal terminated
    // If it has to be, it _must_ be immediately restarted
    // PROBLEM: the time that is slept depends on number of aim calls.
    while((timeLeft > 1) || Stunned){
		//get PRINT(timeLeft, get GAME_FRAME);
        addTime = timeLeft;
        timeLeft = 1;
        sleep addTime;//can sometimes sleep 6000 but never wake up again?
    }
    // The restore section must be atomic :/

	sleep restore_delay;
	if ( NOT (silo_state==3) )
	{
		hide firing;
		silo_state = 4;
		start-script CloseSiloDoors();
	}
	return (0);

    timeLeft = 0;
}







AimWeapon1 (heading, pitch)
{
	signal SIGNAL_AIM1;
	set-signal-mask SIGNAL_AIM1;
	return (1);
}

FireWeapon1 ()
{
	return (1);
}

QueryWeapon1 (piecenum)
{
	piecenum = firing;
	return (0);
}







AimWeapon2 (heading, pitch)
{
	signal SIGNAL_AIM2;
	set-signal-mask SIGNAL_AIM2;
	start-script OpenSilo();
	if (silo_state == 2)
	{
		return (1);
	}
	return (0);
}

FireWeapon2 ()
{
	shotcount = shotcount + 1;
	call-script lua_UnitScriptLight(1, shotcount);
	call-script lua_UnitScriptLight(2, shotcount);
	start-script CloseSilo_Fired();
	return (0);
}

QueryWeapon2 (piecenum)
{
	piecenum = flare;
	return (0);
}




SetStockpile (num_stockpiled)
{
	if (num_stockpiled == 0)
	{
		
		if (NOT firing) {
			hide firing;
			hide firing1;
			hide firing2;
		}
		
		sleep 100;
		missiles_stockpiled = 0;
	}
	if (num_stockpiled >= 1)
	{
		show firing;
		show firing1;
		show firing2;
		sleep 100;
		missiles_stockpiled = 1;
	}
}



Propcontrol ()
{
	var currentSpeed;
	while (1)
	{
		currentSpeed = (get CURRENT_SPEED)*1000/TB_maxSpeed;	
		spin prop around z-axis speed <1.0>*currentSpeed accelerate <10.0>;
		spin propl around z-axis speed <1.0>*currentSpeed accelerate <10.0>;
		spin propr around z-axis speed <1.0>*currentSpeed accelerate <10.0>;
		sleep 100;
	}
}



StartMoving ()
{
	signal SIGNAL_MOVE;
	set-signal-mask SIGNAL_MOVE;
	start-script Propcontrol ();
	while( TRUE )
	{
		emit-sfx 1024 + 0 from wake;
		sleep 250;
		emit-sfx 1024 + 0 from wake;
		emit-sfx 1024 + 1 from wake;
		sleep 250;
	}
}

StopMoving ()
{
	signal SIGNAL_MOVE;
	stop-spin prop around z-axis decelerate <10.0>;
	stop-spin propl around z-axis decelerate <10.0>;
	stop-spin propr around z-axis decelerate <10.0>;
}


Killed (severity, corpsetype)
{
	if ( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if ( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if ( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
	return corpsetype;
}
