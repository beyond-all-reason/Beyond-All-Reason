
#include "../recoil_common_includes.h"
#include "smart_weapon_select.h"

piece  flare1, flare2, base, turret, barrel1, barrel2, sleeves, turretmidpoint;

static-var  nextBarrel, trajectoryMode, lastHeading;

#define SIGNAL_AIM2			1
#define LOW_TRAJECTORY		0
#define HIGH_TRAJECTORY		1


Create()
{
	hide flare1;
	hide flare2;
	hide flare1;
	hide flare2;
	nextBarrel = 0;
}


#define BASEPIECE base
#define MAXTILT 0
#include "../unit_hitbyweaponid_and_smoke.h"

Activate()
{
	signal SIGNAL_AIM2;
	trajectoryMode = HIGH_TRAJECTORY;
}

Deactivate()
{
	signal SIGNAL_AIM2;
	trajectoryMode = LOW_TRAJECTORY;
}

restoreAfterDelay()
{
	set-signal-mask SIGNAL_AIM2;
	sleep 3500;
	if(lastHeading > <-180.0>)
	{
		turn turret to y-axis <180.0> speed <30.0>;
	}
	if(lastHeading > <-135.0>)
	{
		turn turret to y-axis <270.0> speed <30.0>;
	}
	if(lastHeading > <-45.0>)
	{
		turn turret to y-axis <0.0> speed <30.0>;
	}
	if(lastHeading > <45.0>)
	{
		turn turret to y-axis <90.0> speed <30.0>;
	}
	if(lastHeading > <135.0>)
	{
		turn turret to y-axis <180.0> speed <30.0>;
	}
	if (trajectoryMode == LOW_TRAJECTORY)
	{
		turn sleeves to x-axis <0.0> speed <45.0>;
	}
}

aimCommon(heading, pitch)
{
	turn turret to y-axis heading speed <30.0>;
	turn sleeves to x-axis <0.0> - pitch speed <45.0>;
	lastHeading = heading;
	wait-for-turn turret around y-axis;
	wait-for-turn sleeves around x-axis;
	start-script restoreAfterDelay();
}

AimWeapon1(heading, pitch)
{
	if (AimingState != AIMING_BACKUP)
	{
		return(0);
	}

	signal SIGNAL_AIM2;
	set-signal-mask SIGNAL_AIM2;
	call-script aimCommon(heading, pitch);

	// Prevent low-trajectory "bonus shots" while high-trajectory is reloading
	var reloading;
	reloading = (get GAME_FRAME) < (get WEAPON_RELOADSTATE(2));
	if( reloading )
	{
		return(0);
	}

	return (1);
}

AimWeapon2(heading, pitch)
{
	if (AimingState != AIMING_BACKUP)
	{
		return(0);
	}

	signal SIGNAL_AIM2;
	set-signal-mask SIGNAL_AIM2;
	call-script aimCommon(heading, pitch);
	// Prevent "bonus shots" while alternative weapon is reloading
	var reloading;
	reloading = (get GAME_FRAME) < (get WEAPON_RELOADSTATE(1));
	if( reloading )
	{
		return(0);
	}
	return (1);
}

AimWeapon3(pieceIndex)
{
	return (0);
}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = sleeves;
}

AimFromWeapon2(pieceIndex)
{
	pieceIndex = sleeves;
}

AimFromWeapon3(pieceIndex)
{
	pieceIndex = turretmidpoint;
}

fireCommon()
{
	if( nextBarrel == 0 )
	{
	    emit-sfx 1024 + 0 from flare1;
		move barrel1 to z-axis [-6.250000] now;
		sleep 100;
		move barrel1 to z-axis [0.0] speed [2.500000];
	}
	if( nextBarrel == 1 )
	{
	    emit-sfx 1024 + 0 from flare2;
		move barrel2 to z-axis [-6.250000] now;
		sleep 100;
		move barrel2 to z-axis [0.0] speed [2.500000];
	}
	nextBarrel = !nextBarrel;
}

FireWeapon1()
{
	call-script fireCommon();
}

FireWeapon2()
{
	call-script fireCommon();
}


QueryWeapon1(pieceIndex)
{
	if( nextBarrel == 0 )
	{
		pieceIndex = flare1;
	}
	if( nextBarrel == 1 )
	{
		pieceIndex = flare2;
	}
}

QueryWeapon2(pieceIndex)
{
	if( nextBarrel == 0 )
	{
		pieceIndex = flare1;
	}
	if( nextBarrel == 1 )
	{
		pieceIndex = flare2;
	}
}

QueryWeapon3(pieceIndex)
{
	pieceIndex = turretmidpoint;
}


Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret type BITMAPONLY | NOHEATCLOUD;
		explode sleeves type BITMAPONLY | NOHEATCLOUD;
		explode barrel2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare2 type BITMAPONLY | NOHEATCLOUD;
		explode barrel1 type BITMAPONLY | NOHEATCLOUD;
		explode flare1 type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret type FALL | NOHEATCLOUD;
		explode sleeves type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrel2 type FALL | NOHEATCLOUD;
		explode flare2 type FALL | NOHEATCLOUD;
		explode barrel1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare1 type FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode sleeves type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode barrel2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare2 type SMOKE | FALL | NOHEATCLOUD;
		explode barrel1 type SMOKE | FALL | NOHEATCLOUD;
		explode flare1 type SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode sleeves type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrel2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode flare2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrel1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
