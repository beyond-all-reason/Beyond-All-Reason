
#include "../recoil_common_includes.h"

piece  base, thruster, flwing, frwing, blwing, brwing, flblades, frblades, blblades, brblades, blades, flare, ringazimuth, ringelevation, armor, thrusttrail;

static-var shotcount;

#define SIGNAL_AIM1 256

#define IDLEHOVERSCALE 24
#define IDLEHOVERSPEED 30
#define IDLEBASEPIECE base
#include "../idlehover.h"

#define BASEPIECE base
#define HITSPEED <105.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 5
#define MAXTILT 100

Create()
{
	hide flare;
	
	turn flwing to y-axis <-30> now;
	turn frwing to y-axis <30> now;
	turn blwing to y-axis <30> now;
	turn brwing to y-axis <-30> now;
	
	turn ringazimuth to y-axis <0> now;
	turn ringelevation to x-axis <-90> now;
	
	SLEEP_UNTIL_UNITFINISHED;
	call-script Activate();
	start-script IdleHover();
}

Activate()
{
	spin flblades around y-axis speed <600>;
	spin frblades around y-axis speed <-600>;
	spin blblades around y-axis speed <-600>;
	spin brblades around y-axis speed <600>;
	spin blades around z-axis speed <200>;
	
	turn flwing to z-axis <-15> speed <120>;
	turn frwing to z-axis <15> speed <120>;
	turn blwing to z-axis <-15> speed <120>;
	turn brwing to z-axis <15> speed <120>;
	
	move base to y-axis [0] speed [1];
	
	turn thruster to x-axis <0> speed <90>;
	show thrusttrail;
}

Deactivate()
{
	stop-spin flblades around y-axis decelerate <10>;
	stop-spin frblades around y-axis decelerate <10>;
	stop-spin blblades around y-axis decelerate <10>;
	stop-spin brblades around y-axis decelerate <10>;
	stop-spin blades around z-axis decelerate <10>;
	
	turn flwing to z-axis <45> speed <180>;
	turn frwing to z-axis <-45> speed <180>;
	turn blwing to z-axis <45> speed <180>;
	turn brwing to z-axis <-45> speed <180>;
	
	move base to y-axis [2] speed [20];
	
	turn thruster to x-axis <-90> speed <90>;
	hide thrusttrail;
	
	turn ringazimuth to y-axis <0> speed <30>;
	turn ringelevation to x-axis <-90> speed <30>;
}

QueryWeapon1(pieceIndex)
{
	pieceIndex = flare;
}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = base;
}

AimWeapon1(heading, pitch)
{
    signal SIGNAL_AIM1;
	set-signal-mask SIGNAL_AIM1;
	
	turn thruster to x-axis <0> speed <90>;
	
	turn ringazimuth to y-axis heading speed <300>;
	turn ringelevation to x-axis 0 - pitch speed <300>;
	
	return (1);
}

lua_UnitScriptLight(lightIndex, count)
{
	return 0;
}

FireWeapon1()
{
	++shotcount;
	emit-sfx 1024 + 0 from flare;
	call-script lua_UnitScriptLight(1, shotcount);
}

MoveRate0()
{
	turn thruster to x-axis <-90.0> speed <180.0>;
}

MoveRate1()
{
	turn thruster to x-axis <-45.0> speed <180.0>;
}

MoveRate2()
{
	turn thruster to x-axis <0.0> speed <180.0>;
}

MoveRate3()
{
	turn thruster to x-axis <0.0> speed <180.0>;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode armor type BITMAPONLY | NOHEATCLOUD;
		explode flwing type BITMAPONLY | NOHEATCLOUD;
		explode frwing type BITMAPONLY | NOHEATCLOUD;
		explode thruster type BITMAPONLY | NOHEATCLOUD;
		explode ringazimuth type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode ringelevation type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode armor type BITMAPONLY | NOHEATCLOUD;
		explode flwing type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode frwing type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode thruster type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode ringazimuth type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode ringelevation type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode armor type BITMAPONLY | NOHEATCLOUD;
		explode flwing type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode frwing type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode thruster type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode ringazimuth type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode ringelevation type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode armor type BITMAPONLY | NOHEATCLOUD;
		explode flwing type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode frwing type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode thruster type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode ringazimuth type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode ringelevation type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
	return corpsetype;
}
