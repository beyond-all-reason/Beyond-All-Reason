
#include "../recoil_common_includes.h"

piece  ground, lflare, rflare, turret, wake, barrelcon, lbarrel, rbarrel, launch, lrocket,
       rrocket, base;

static-var  gun_1, isAiming, restore_delay;

// Signal definitions
#define SIGNAL_AIM1 256
#define SIGNAL_AIM2 512 

#define HOVER_BASE base
#define HOVER_BANKSPEED <7>
#define HOVER_ROCKBASE ground
#define HOVER_WOBBLE_PERIOD 80
#define HOVER_WOBBLE_AMPLITUDE [0.7]
#define HOVER_WAKEPIECE wake
#define HOVER_WAKE_SFX_1 1024 + 1
#define HOVER_WAKE_SFX_2 1024 + 2
#define HOVER_IDLE_SFX 1024 + 3
#include "../bar_hovercraft_common.h"


#define BASEPIECE base
#define HITSPEED <30.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 8
#define MAXTILT 200

#include "../unit_hitbyweaponid_and_smoke.h"


Create()
{
	hide barrelcon;
	hide lrocket;
	hide rrocket;
	hide wake;
	hide lflare;
	hide rflare;
	turn lflare to y-axis <175> now;
	turn rflare to y-axis <175> now;
	move lflare to z-axis [6] now;
	move rflare to z-axis [6] now;
	isAiming = FALSE;
	gun_1 = lflare;
	hide lrocket;
	hide rrocket;
	hide launch;
	restore_delay = 3000;
	start-script HoverCraftMotion();
}

SetMaxReloadTime(reloadMS)
{
	restore_delay = reloadMS * 2;
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	isAiming = FALSE;
	turn turret to y-axis <0.0> speed <80.0>;
	turn barrelcon to x-axis <0.0> speed <50.0>;
}
SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

RestoreWeapon2()
{
	sleep restore_delay;
	isAiming = FALSE;
	turn launch to x-axis <0.0> speed <20.0>;
}

AimWeapon1(heading, pitch)
{
	signal SIGNAL_AIM1;
	set-signal-mask SIGNAL_AIM1;
	isAiming = TRUE;
	turn turret to y-axis heading speed <120.0>;
	turn barrelcon to x-axis <0.0> - pitch speed <80.0>;
	wait-for-turn turret around y-axis;
	wait-for-turn barrelcon around x-axis;
	start-script RestoreAfterDelay();
	return (1);
}

FireWeapon1()
{
	if( !gun_1 )
	{
		emit-sfx 1024 + 0 from rflare;
		move rbarrel to z-axis [-5.0] now;
		move rbarrel to z-axis [0.0] speed [7.50000];
	}
	if( gun_1 )
	{
		emit-sfx 1024 + 0 from lflare;
		move lbarrel to z-axis [-5.0] now;
		move lbarrel to z-axis [0.0] speed [7.50000];
	}
	gun_1 = !gun_1;
	return (0);
}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = turret;
}

QueryWeapon1(pieceIndex)
{
	pieceIndex = gun_1;
}

QueryWeapon2(pieceIndex)
{
	pieceIndex = base;
}

AimFromWeapon2(pieceIndex)
{
	pieceIndex = base;
}

FireWeapon2()
{	
}

AimWeapon2(heading, pitch)
{
	signal SIGNAL_AIM2;
	set-signal-mask SIGNAL_AIM2;
	start-script RestoreAfterDelay();
	return (1);
}



Killed(severity, corpsetype)
{
	hide lflare;
	hide rflare;

	if( severity <= 25 )
	{
		corpsetype = 1;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return (corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode lbarrel type FALL | BITMAP1 | NOHEATCLOUD;
		explode rbarrel type FALL | BITMAP1 | NOHEATCLOUD;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode launch type FALL | BITMAP3 | NOHEATCLOUD;
		explode turret type BITMAP4 | NOHEATCLOUD;
		return (corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3;
		explode lbarrel type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1 | NOHEATCLOUD;
		explode rbarrel type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1 | NOHEATCLOUD;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode launch type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP3 | NOHEATCLOUD;
		explode turret type FIRE | SMOKE | FALL | BITMAP4 | NOHEATCLOUD;
		return (corpsetype);
	}
	corpsetype = 3;
	explode lbarrel type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1 | NOHEATCLOUD;
	explode rbarrel type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1 | NOHEATCLOUD;
	explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
	explode launch type EXPLODE_ON_HIT | BITMAP4 | NOHEATCLOUD;
	explode turret type EXPLODE_ON_HIT | BITMAP4 | NOHEATCLOUD;
	return (corpsetype);
}
