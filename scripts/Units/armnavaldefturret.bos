
#include "../recoil_common_includes.h"

piece
base,
turretHeadingPivot,
turret,
turretPitchPivot,
leftBarrel,
rightBarrel,
rightFlare,
leftFlare,
turretPlating;



static-var restore_delay, wpn1_lasthead, whichBarrel;

// Signal definitions
#define SIG_AIM				2

#define WATER_ROCK_UNITSIZE 10
#include "../floatmotion.h"


#define BASEPIECE base
#define HITSPEED <20.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 5
#define MAXTILT 100
#define RECOIL_POWER 20000

#include "unit_hitbyweaponid_and_smoke.h"


Create()
{
    whichBarrel = 0;
	restore_delay = 2000;
	start-script FloatMotion();
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	turn turretHeadingPivot to y-axis <0> speed <60>;
    turn turretPitchPivot to x-axis <0.000000> speed <30>;
	wpn1_lasthead = 1000000;
	set-signal-mask 0;
}

SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}

RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turretHeadingPivot to y-axis heading speed <150>;
	turn turretPitchPivot to x-axis <0.000000> - pitch speed <80>;


	if (((get ABS(wpn1_lasthead - heading)) > 65536) OR(((get ABS(wpn1_lasthead - heading)) > 1300) AND ((get ABS(wpn1_lasthead - heading)) < 64236)))
	{
		wpn1_lasthead = 1000000;
		wait-for-turn turretHeadingPivot around y-axis;
		wait-for-turn turretPitchPivot around x-axis;
	}
	wpn1_lasthead = heading;
	start-script RestoreAfterDelay();
	return (1);
}

FirePrimary()
{
    if (whichBarrel == 0){
        emit-sfx 1024 + 0 from rightFlare;
        move rightbarrel to z-axis [-6] now;
        sleep 10;
        move rightbarrel to z-axis [0] speed [4];
        whichBarrel = 1;
    }
    else if (whichBarrel == 1){
        emit-sfx 1024 + 0 from leftFlare;
        move leftBarrel to z-axis [-6] now;
        sleep 10;
        move leftBarrel to z-axis [0] speed [4];
        whichBarrel = 0;
    }
    else{
        whichBarrel = 0;
    }
}


AimFromPrimary(piecenum)
{
	piecenum = turret;
}

QueryPrimary(piecenum)
{
    if (whichBarrel == 0){
        piecenum = rightFlare;
    }
    if (whichBarrel == 1){
        piecenum = leftFlare;
    }
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turretHeadingPivot type BITMAPONLY | NOHEATCLOUD;
		explode turretPitchPivot type BITMAPONLY | NOHEATCLOUD;
		explode rightBarrel type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rightFlare type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turretHeadingPivot type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turretPitchPivot type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rightBarrel type FALL | NOHEATCLOUD;
		explode rightFlare type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turretHeadingPivot type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turretPitchPivot type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rightBarrel type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rightFlare type SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turretHeadingPivot type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turretPitchPivot type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode rightBarrel type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode rightFlare type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
