
#include "../recoil_common_includes.h"
#include "../factories_common.h"

piece  base, pad, ventsmoke1, ventsmoke2, cylinder1, cylinder2, domelight, domelight_emit, lrail, rrail, lpivot, rpivot, larm, rarm, lnano1, lnano2, rnano1, rnano2, beam1, beam2, beam3, beam4;

static-var  spray;

// Signal definitions
#define SIGNAL_BUILD 2
#define SIGNAL_TURNON 4


#define WATER_ROCK_UNITSIZE 27
#define WATER_ROCK_AMPLITUDE <1.2>
#include "../floatmotion.h"


#define BASEPIECE base
#define HITSPEED <10.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 15
#define MAXTILT 100
#include "../unit_hitbyweaponid_and_smoke.h"

Create()
{
	hide beam3;
	hide beam1;
	hide beam4;
	hide beam2;
	hide pad;
	hide domelight_emit;

	spray = 0;
	SLEEP_UNTIL_UNITFINISHED;
	start-script FloatMotion();
}

QueryNanoPiece(pieceIndex)
{
	pieceIndex=beam1+spray;
	spray=spray+1;
	if (spray>3) spray=0;
}

MoveCranes()
{
	while(TRUE)
	{
		turn rpivot to y-axis rand(-6000, 6000) speed <45>;
		turn lnano1 to y-axis rand(0, 3000) speed <45>;
		turn lnano2 to y-axis rand(-3000, 0) speed <45>;
		
		sleep(400);
		emit-sfx 257 from ventsmoke1;
		sleep(400);
		emit-sfx 257 from ventsmoke2;
		
		turn lpivot to y-axis rand(-6000, 6000) speed <45>;
		turn rnano1 to y-axis rand(0, 3000) speed <45>;
		turn rnano2 to y-axis rand(-3000, 0) speed <45>;
		
		sleep(400);
		emit-sfx 257 from ventsmoke1;
		sleep(400);
		emit-sfx 257 from ventsmoke2;
	}
}

StartBuilding()
{
	show beam1;
	show beam2;
	show beam3;
	show beam4;
	
	show domelight_emit;
	move domelight to y-axis [2] speed [4];
	spin domelight around y-axis speed <250> accelerate <2>;
	
	signal SIGNAL_BUILD;
	set-signal-mask SIGNAL_BUILD;
	start-script MoveCranes();
}

StopBuilding()
{
	hide beam1;
	hide beam2;
	hide beam3;
	hide beam4;
	
	hide domelight_emit;
	move domelight to y-axis [0] speed [4];
	stop-spin domelight around y-axis decelerate <2>;
	
	turn lpivot to y-axis <0> speed <45>;
	turn rpivot to y-axis <0> speed <45>;
	
	turn lnano1 to y-axis <0> speed <45>;
	turn lnano2 to y-axis <0> speed <45>;
	turn rnano1 to y-axis <0> speed <45>;
	turn rnano2 to y-axis <0> speed <45>;
	
	signal SIGNAL_BUILD;
}

Activate()
{
	signal SIGNAL_TURNON;
	set-signal-mask SIGNAL_TURNON;
	turn larm to x-axis <-90> speed <90>;
	turn rarm to x-axis <90> speed <90>;
	wait-for-turn rarm around x-axis;
	
	FACTORY_OPEN_BUILD;
}

Deactivate()
{
	signal SIGNAL_TURNON;
	set-signal-mask SIGNAL_TURNON;
	
	sleep 5000;

	turn larm to x-axis <0> speed <90>;
	turn rarm to x-axis <0> speed <90>;
	wait-for-turn rarm around x-axis;

	FACTORY_CLOSE_BUILD;
}

QueryBuildInfo(pieceIndex)
{
	pieceIndex = pad;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode lnano1 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode lnano2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode lrail type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode lpivot type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode lnano1 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode lnano2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode lrail type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode lpivot type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode lnano1 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode lnano2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode lrail type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode lpivot type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode lnano1 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode lnano2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode lrail type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode lpivot type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
	return corpsetype;
}
