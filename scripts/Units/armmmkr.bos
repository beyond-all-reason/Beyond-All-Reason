#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, cover1, cover2;

static-var  mmState, Active;

activatescr()
{
	turn cover1 to z-axis <60> speed <60>;
	turn cover2 to z-axis <-60> speed <60>;
	sleep 1000;
}

deactivatescr()
{
	turn cover1 to z-axis <0> speed <60>;
	turn cover2 to z-axis <0> speed <60>;
	sleep 1000;
}


Go()
{
	call-script activatescr();
	set ARMORED to 0; //Not needed as MM in BAR have no damage modifyer tag set
}

Stop()
{
	set ARMORED to 1;
	call-script deactivatescr();
}

ImAMetalMaker()
{
	mmState = -1;
	while( TRUE )
	{
		if (Active == 1)
		{
			if (mmState != 1) {
				call-script Go();
				mmState = 1;
			}
		}
		else
		{
			if (mmState != 0) {
				call-script Stop();
				mmState = 0;
			}
		}
		sleep 1000;
	}
}

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	start-script ImAMetalMaker();
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
}

MMStatus(State)
{
	Active = State;
	//get PRINT(Active);
}

Create()
{
	start-script SmokeUnit();
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode cover1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode cover2 type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode cover1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode cover2 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode cover1 type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode cover2 type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode cover1 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode cover2 type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
	return corpsetype;
}
