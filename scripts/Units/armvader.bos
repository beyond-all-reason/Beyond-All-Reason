
#include "../recoil_common_includes.h"

piece  leg1, leg2, leg3, leg4, base, body;

static-var  isMoving;

#define SIGNAL_MOVE 1


#define BASEPIECE base
#define HITSPEED <60.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 1
#define MAXTILT 100

#include "../unit_hitbyweaponid_and_smoke.h"

Create()
{
	hide base;
	isMoving = FALSE;
	SLEEP_UNTIL_UNITFINISHED;
}


StartMoving(reversing)
{
	signal SIGNAL_MOVE;
	move leg1 to y-axis [0] speed [28];
	move leg2 to y-axis [0] speed [28];
	move base to y-axis [0] speed [28];
	spin base around x-axis speed <270> accelerate <270>;
	sleep 150;
	move leg3 to y-axis [0] speed [28];
	move leg4 to y-axis [0] speed [28];
	isMoving = TRUE;
}

StopMoving()
{
	set-signal-mask SIGNAL_MOVE;
	stop-spin base around x-axis;
	turn base to x-axis <0> speed <270>;
	wait-for-turn base around x-axis;	
	move leg1 to y-axis [-2.5] speed [18];
	move leg2 to y-axis [-2.5] speed [18];
	move leg4 to y-axis [-2.5] speed [18];
	move leg3 to y-axis [-2.5] speed [18];
	move base to y-axis [2.5] speed [18];

	
	isMoving = FALSE;
}

AimWeapon1()
{
	return (TRUE);
}

AimWeapon2()
{
	return (TRUE);
}

FireWeapon1()
{
	get KILL_UNIT(get MY_ID,TRUE,FALSE);
}

QueryWeapon1(pieceIndex)
{
	pieceIndex = base;
}

AimFromWeapon1(pieceIndex)
{
	pieceIndex = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		return(corpsetype);
	}
	corpsetype = 3 ;
	return corpsetype;
}
