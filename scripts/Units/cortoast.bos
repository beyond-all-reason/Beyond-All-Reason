
#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, flare, turret, door1, door2, barrel, bore, aimpoint;

static-var  turretOpen, restoreDelay, stunned, trajectoryMode;
#define SIGNAL_AIM			1
#define LOW_TRAJECTORY		0
#define HIGH_TRAJECTORY		1

openTurret()
{
	turn door1 to z-axis <0> speed <300>;
	turn door2 to z-axis <0> speed <300>;
	wait-for-turn door1 around z-axis;
	sleep 100;

	move door1 to y-axis [0] speed [17];
	move door2 to y-axis [0] speed [17];
	move turret to y-axis [0] speed [17];
	sleep 700;

	move bore to z-axis [0] speed [12];
	move barrel to z-axis [0] speed [18];
	sleep 316;

	sleep 10;
	return (0);
}

closeTurret()
{ 
	turn turret to y-axis <0> speed <100>;
	move bore to z-axis [-6] speed [12];
	move barrel to z-axis [-9] speed [18];
	sleep 316;

	move door1 to y-axis [17] speed [17];
	move door2 to y-axis [17] speed [17];
	move turret to y-axis [-17] speed [17];
	sleep 700;

	turn door1 to z-axis <-90> speed <300>;
	turn door2 to z-axis <90> speed <300>;
	wait-for-turn door1 around z-axis;
	sleep 100;

	sleep 10;
	return (0);;
}

Go()
{
	if( !turretOpen )
	{
		call-script openTurret();
	}
	turretOpen = 1;
	set ARMORED to 0;
	return (0);
}

Stop()
{
	set ARMORED to 1;
	turretOpen = 0;
	call-script closeTurret();
	return (0);
}

RequestState(requestedstate)
{
	trajectoryMode = requestedstate;
}

ExecuteRestoreAfterDelay()
{
    if (stunned)
	{
        return (1);
    }

	set-signal-mask 0;
	turn turret to y-axis <0.000000> speed <100.016484>;
	turn turret to x-axis <0.000000> speed <50.005495>;
	wait-for-turn turret around y-axis;
	wait-for-turn turret around x-axis;
	
	start-script Stop();
}

SetStunned(State)
{
    stunned = State;
	if (!stunned)
	{
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	set-signal-mask SIGNAL_AIM;
	sleep restoreDelay;
	start-script ExecuteRestoreAfterDelay();
}

Create()
{
	hide aimpoint;
	hide flare;
	hide flare;
	turretOpen = 0;
	restoreDelay = 10000;

	while( get BUILD_PERCENT_LEFT )
	{
		sleep 1000;
	}

	start-script RestoreAfterDelay();
	return (0);
}

#define SMOKEPIECE base
#include "smokeunit_thread_nohit.h"

Activate()
{
	signal SIGNAL_AIM;
	start-script RequestState(HIGH_TRAJECTORY);
}

Deactivate()
{
	signal SIGNAL_AIM;
	start-script RequestState(LOW_TRAJECTORY);
}

aimCommon(heading, pitch)
{
	while( turretOpen == 0 )
	{
		sleep 250;
	}

	turn turret to y-axis heading speed <30.000000>;
	turn turret to x-axis <0.000000> - pitch speed <45.000000>;
	wait-for-turn turret around y-axis;
	wait-for-turn turret around x-axis;
}

AimPrimary(heading, pitch)
{
	start-script Go();

	if(trajectoryMode != LOW_TRAJECTORY)
	 {
	 	return(0);
	 }
	
	signal SIGNAL_AIM;
	set-signal-mask SIGNAL_AIM;

	call-script aimCommon(heading, pitch);
	start-script RestoreAfterDelay();

	// Prevent low-trajectory "bonus shots" while high-trajectory is reloading
	var reloading;
	reloading = (get GAME_FRAME) < (get WEAPON_RELOADSTATE(2));
	if( reloading )
	{
		return(0);
	}
	
	return (1);
}

AimSecondary(heading, pitch)
{
	start-script Go();

	if(trajectoryMode != HIGH_TRAJECTORY)
	{
		return(0);
	}

	signal SIGNAL_AIM;
	set-signal-mask SIGNAL_AIM;

	call-script aimCommon(heading, pitch);
	start-script RestoreAfterDelay();
	return (1);
}

fireCommon()
{
	emit-sfx 1024 + 0 from flare;
	move barrel to z-axis [-3.000000] speed [375.000000];
	move bore to z-axis [-2.000000] speed [250.000000];
	sleep 150;
	move barrel to z-axis [0.000000] speed [2.250000];
	move bore to z-axis [0.000000] speed [1.500000];
}

FirePrimary()
{
	call-script fireCommon();
}

FireSecondary()
{
	call-script fireCommon();
}

AimFromPrimary(piecenum)
{
	piecenum = aimpoint;
}

AimFromSecondary(piecenum)
{
	piecenum = aimpoint;
}

QueryPrimary(piecenum)
{
	piecenum = flare;
}

QuerySecondary(piecenum)
{
	piecenum = flare;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode door1 type BITMAPONLY | NOHEATCLOUD;
		explode door2 type BITMAPONLY | NOHEATCLOUD;
		explode turret type BITMAPONLY | NOHEATCLOUD;
		explode bore type BITMAPONLY | NOHEATCLOUD;
		explode barrel type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode door1 type FALL | NOHEATCLOUD;
		explode door2 type FALL | NOHEATCLOUD;
		explode turret type FALL | NOHEATCLOUD;
		explode bore type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode barrel type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare type FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode door1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode door2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode bore type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode barrel type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode door1 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode door2 type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode bore type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode barrel type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode flare type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
	return corpsetype;
}
