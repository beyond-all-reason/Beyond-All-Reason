#define TA			// This is a TA script

#include "sfxtype.h"
#include "exptype.h"

piece  base, turret, gun, sleeve, lgun, lgatling, rgun, rgatling, flare, lflare, rflare;

static-var  restore_delay, last_gun_heading;

// Signal definitions
#define SIG_AIM				2
#define SIG_AIM_2			4
#define SIG_AIM_3			8
#define SIG_FIRE1 			16
#define SIG_FIRE2 			32

#define BASEPIECE base
#define HITSPEED <85.0>
//how 'heavy' the unit is, on a scale of 1-10
#define UNITSIZE 7
#define MAXTILT 100

StartMoving()
{
	turn base to x-axis <-3> speed <11>;
	wait-for-turn base around x-axis;
	turn base to x-axis <0> speed <11>;
}

StopMoving()
{
	turn base to x-axis <1> speed <5>;
	wait-for-turn base around x-axis;
	turn base to x-axis <0> speed <5>;
}

Create()
{
	hide flare;
	hide rflare;
	hide lflare;
	restore_delay = 3000;
}

SetMaxReloadTime(Func_Var_1)
{
	restore_delay = Func_Var_1 * 2;
}

static-var  Stunned;
ExecuteRestoreAfterDelay()
{
    if (Stunned) {
        return (1);
    }
	set-signal-mask 0;
	turn turret to y-axis <0.000000> speed <90.000000>;
	turn gun to x-axis <0.000000> speed <50.000000>;
	turn lgun to x-axis <0.000000> speed <50.000000>;
	turn rgun to x-axis <0.000000> speed <50.000000>;
	stop-spin lgatling around z-axis decelerate <10.000000>;
	stop-spin rgatling around z-axis decelerate <10.000000>;
}
SetStunned(State)
{
    Stunned = State;
	if (!Stunned) {
	    start-script ExecuteRestoreAfterDelay();
	}
}
RestoreAfterDelay()
{
	sleep restore_delay;
	start-script ExecuteRestoreAfterDelay();
}

AimPrimary(heading, pitch)
{
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	turn turret to y-axis heading speed <90.000000>;
	turn gun to x-axis <0.000000> - pitch speed <50.000000>;
	wait-for-turn turret around y-axis;
	wait-for-turn gun around x-axis;
	start-script RestoreAfterDelay();
	last_gun_heading = heading;
	return (1);
}

Rock_Main_Cannon()
{
	turn base to z-axis <0> - (GET KSIN(last_gun_heading)) speed <50.005495>;
	turn base to x-axis <0> - (GET KCOS(last_gun_heading)) speed <50.005495>;
	wait-for-turn base around z-axis;
	wait-for-turn base around x-axis;
	turn base to z-axis <0.000000> speed <20.000000>;
	turn base to x-axis <0.000000> speed <20.000000>;
}

FirePrimary()
{
	emit-sfx 1024 + 0 from flare;
	start-script Rock_Main_Cannon();
	move sleeve to z-axis [-2.400000] speed [500.000000];
	wait-for-move sleeve along z-axis;
	move sleeve to z-axis [0.000000] speed [3.000000];
}

AimFromPrimary(piecenum)
{
	piecenum = gun;
}

QueryPrimary(piecenum)
{
	piecenum = flare;
}

AimSecondary(heading, pitch)
{
	signal SIG_AIM_2;
	set-signal-mask SIG_AIM_2;
	turn lgun to x-axis <0.000000> - pitch speed <50.000000>;
	
	start-script RestoreAfterDelay();
	return (1);
}

FireSecondary()
{
	spin lgatling around z-axis speed <900.000000>;
	return (1);
}

Shot2(zero)
{
	signal SIG_FIRE1;
	set-signal-mask SIG_FIRE1;
	emit-sfx 1024 + 1 from lflare;
	return(1);
}

AimFromSecondary(piecenum)
{
	piecenum = lgun;
}

QuerySecondary(piecenum)
{
	piecenum = lflare;
}

AimTertiary(heading, pitch)
{
	signal SIG_AIM_3;
	set-signal-mask SIG_AIM_3;
	turn rgun to x-axis <0.000000> - pitch speed <50.000000>;
	
	start-script RestoreAfterDelay();
	return (1);
}

FireTertiary()
{
	spin rgatling around z-axis speed <-900.000000>;
	return (1);
}

Shot3(zero)
{
	signal SIG_FIRE2;
	set-signal-mask SIG_FIRE2;
	emit-sfx 1024 + 1 from rflare;
	return(1);
}

AimFromTertiary(piecenum)
{
	piecenum = rgun;
}

QueryTertiary(piecenum)
{
	piecenum = rflare;
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret type BITMAPONLY | NOHEATCLOUD;
		//explode gun type BITMAPONLY | NOHEATCLOUD;
		explode sleeve type FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode flare type BITMAPONLY | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 50 )
	{
		corpsetype = 2 ;
		explode base type BITMAPONLY | NOHEATCLOUD;
		explode turret type FALL | NOHEATCLOUD;
		explode gun type FALL | NOHEATCLOUD;
		explode sleeve type FIRE | SMOKE | FALL | NOHEATCLOUD;
		//explode flare type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	if( severity <= 99 )
	{
		corpsetype = 3 ;
		explode base type FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		explode gun type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode sleeve type EXPLODE_ON_HIT | SMOKE | FALL | NOHEATCLOUD;
		//explode flare type FIRE | SMOKE | FALL | NOHEATCLOUD;
		return(corpsetype);
	}
	corpsetype = 3 ;
		explode base type EXPLODE_ON_HIT | FIRE | SMOKE | FALL | NOHEATCLOUD;
		explode turret type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode gun type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		explode sleeve type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
		//explode flare type EXPLODE_ON_HIT | FIRE | FALL | NOHEATCLOUD;
	return corpsetype;
}